apply plugin: 'maven'
apply plugin: 'signing'

group = 'org.fxmisc.livedirs'

dependencies {
    compile group: 'org.reactfx', name: 'reactfx', version: '2.0-M5'
}

javadoc {
    // ignore missing Javadoc comments or tags
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

signArchives.onlyIf {
    project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')
}

def doUploadArchives = project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')

if(doUploadArchives) {
    uploadArchives {
        repositories.mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'LiveDirsFX'
                packaging 'jar'
                description 'Directory tree model for JavaFX that watches the filesystem for changes.'
                url 'http://www.fxmisc.org/livedirs/'

                scm {
                    url 'scm:git@github.com:TomasMikula/LiveDirsFX.git'
                    connection 'scm:git@github.com:TomasMikula/LiveDirsFX.git'
                    developerConnection 'scm:git@github.com:TomasMikula/LiveDirsFX.git'
                }

                licenses {
                    license {
                        name 'The BSD 2-Clause License'
                        url 'http://opensource.org/licenses/BSD-2-Clause'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'Tomas Mikula'
                    }
                }
            }
        }
    }
}

uploadArchives.onlyIf { doUploadArchives }

task fatJar(type: Jar, dependsOn: classes) {
    appendix = 'fat'
    from sourceSets.main.output
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

assemble.dependsOn fatJar
